/*
 * generated by Xtext
 */
package com.devoxx2012.xtext.generator

import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.IGenerator
import org.eclipse.xtext.generator.IFileSystemAccess
import com.devoxx2012.xtext.izpack.Installer
import com.devoxx2012.xtext.izpack.Author
import com.devoxx2012.xtext.izpack.ISO3Code
import com.devoxx2012.xtext.izpack.Panel
import com.devoxx2012.xtext.izpack.Pack
import com.devoxx2012.xtext.izpack.File
import com.devoxx2012.xtext.izpack.OS

class IzpackGenerator implements IGenerator {
	def xmlName(Resource res) {
		var name = res.URI.lastSegment
		return name.substring(0, name.indexOf('.')).concat(".xml")
	}
	override void doGenerate(Resource resource, IFileSystemAccess fsa) {
		fsa.generateFile(resource.xmlName, gen(resource.contents.head as Installer))
	}
	
	def gen(Installer installer) '''
		 <?xml version="1.0" encoding="UTF-8"?>
		 <installation version="1.0">
		   <info>
		     <appname>«installer.identification.name»</appname>
		     <appversion>«installer.identification.version»</appversion>
		     «IF installer.identification.url != null»
		     <url>«installer.identification.url»</url>
		     «ENDIF»
		     <authors>
		     «FOR Author author : installer.authors»
		       <author name="«author.name»" email="«author.email»"/>
		     «ENDFOR»
		     </authors>
		     «IF installer.uninstall != null»
		     <uninstaller name="«installer.uninstall.name»" path="«installer.uninstall.path»"/>
		     «ENDIF»
		     «IF installer.constraint?.javaversion != null»
		     <javaversion>«installer.constraint.javaversion»</javaversion>
		     «ENDIF»
		     «IF installer.identification.webdir != null»
		     <webdir>«installer.identification.webdir»</webdir>
		     «ENDIF»
		     «IF installer.summarylog != null»
		     <summarylogfile>«installer.summarylog»</summarylogfile>
		     «ENDIF»
		     <writeinstallationinformation>«IF installer.installation»true«ELSE»false«ENDIF»</writeinstallationinformation>
		     «IF installer.constraint?.packed»
		     <pack200/>
		     «ENDIF»
		   </info>
		   <locale>
		   «FOR ISO3Code code : installer.locale.isocodes»
		     <langpack iso3="«code.name»"/>
		   «ENDFOR»
		   </locale>
		   <panels>
		   «FOR Panel panel : installer.panels»
		     <panel id="«panel.name»" classname="«panel.classname.identifier»" jar="«panel.jar»"/>
		   «ENDFOR»
		   </panels>
		   <packs>
		   «FOR Pack pack : installer.packs»
		     <pack id="«pack.name»" name="«pack.label»" «IF pack.os !=OS::ALL»os="«pack.os.name»" «ENDIF»required="«IF pack.optional»no«ELSE»yes«ENDIF»" loose="«pack.loose»" uninstall="«IF pack.keep»no«ELSE»yes«ENDIF»">
		       <description/>
		       «FOR File file : pack.files»
		       «IF file.alias != null»
		       <singlefile src="«file.path»" target="«file.destination»/«file.alias»"/>
		       «ELSE»
		       <file src="«file.path»" targetdir="«file.destination»"/>
		       «ENDIF»
		       «ENDFOR»
		     </pack>
		   «ENDFOR»
		   </packs>
		   <guiprefs resizable="yes" width="800" height="600"/>
		 </installation>
	'''
	
}
